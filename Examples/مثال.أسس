اشمل "مـتم/مـصفوفة"؛
اشمل "مـتم/ذاكـرة"؛
اشمل "مـتم/طـرفية"؛
اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/Ggml"، "جـجمل.أسس")؛
استخدم مـتم؛
استخدم جـجمل؛

دالة رئيسية() {
    // تهيئة بيانات المصفوفات لإجراء عملية ضرب المصفوفات
    عرف صفوف_أ: صحيح = 4؛
    عرف أعمدة_أ: صحيح = 2؛
    عرف المصفوفة_أ: مـصفوفة[عائم]({
        2.0، 8.0،
        5.0، 1.0،
        4.0، 2.0،
        8.0، 6.0
    })؛

    عرف صفوف_ب: صحيح = 3؛
    عرف أعمدة_ب: صحيح = 2؛
    عرف المصفوفة_ب: مـصفوفة[عائم]({
        10.0، 5.0،
        9.0، 9.0،
        5.0، 4.0
    })؛

    // 1. حجز سياق جـجمل لتخزين بيانات الموترات
    // حساب الحجم المطلوب للحجز
    عرف حجم_السياق: طـبيعي_متكيف = 0؛
    حجم_السياق += صفوف_أ * أعمدة_أ * هات_حجم_النوع(نـوع._ع32_)؛ // الموتر أ (ع32 = 4 بايتات)
    حجم_السياق += صفوف_ب * أعمدة_ب * هات_حجم_النوع(نـوع._ع32_)؛ // الموتر ب (ع32 = 4 بايتات)
    حجم_السياق += صفوف_أ * صفوف_ب * هات_حجم_النوع(نـوع._ع32_)؛ // الناتج (ع32 = 4 بايتات)
    حجم_السياق += 3 * هات_تكلفة_الموتر()؛ // البيانات الوصفية لـ 3 موترات (تكلفة تقريبية)
    حجم_السياق += هات_تكلفة_البيان()؛ // تكلفة البيان
    حجم_السياق += 1024؛ // بعض التكلفة الإضافية

    // حجز سياق جـجمل لتخزين بيانات الموترات
    عرف السياق: سند[سـياق](هيئ(مـعطيات_تهيئة().{
        حجم_الذاكرة = حجم_السياق؛
        صوان_الذاكرة = 0؛
        بلا_حجز = خطأ؛
    }))؛

    // 2. إنشاء الموترات وتعيين البيانات
    عرف الموتر_أ: سند[مـوتر](السياق.أنشئ_موترا(نـوع._ع32_، أعمدة_أ، صفوف_أ))؛
    عرف الموتر_ب: سند[مـوتر](السياق.أنشئ_موترا(نـوع._ع32_، أعمدة_ب، صفوف_ب))؛

    // نسخ بيانات المصفوفات إلى الموترات
    ذاكـرة.انسخ(الموتر_أ.البيانات، المصفوفة_أ.صوان~مؤشر، الموتر_أ.عدد_البايتات)؛
    ذاكـرة.انسخ(الموتر_ب.البيانات، المصفوفة_ب.صوان~مؤشر، الموتر_ب.عدد_البايتات)؛

    // 3. إنشاء بيان لعملية ضرب المصفوفات
    عرف البيان: سند[بـيان](السياق.أنشئ_بيانا())؛

    // الناتج = أ * ب^ت
    // ملاحظة: اضرب_مصفوفات(أ، ب) ==> سيتم قلب ب داخلياً
    // الناتج منقول
    عرف الناتج: سند[مـوتر](السياق.اضرب_مصفوفات(الموتر_أ، الموتر_ب))؛

    // تحديد موتر "الناتج" ليتم حسابه
    ابن_توسيعا_أماميا(البيان، الناتج)؛

    البيان.اطبع()؛

    // 4. تشغيل الحساب
    عرف عدد_المسالك: صحيح = 1؛ // عدد المسالك المستخدمة للحساب
    السياق.شغل_البيان(البيان، عدد_المسالك)؛

    // 5. استرجاع النتائج (موترات الإخراج)
    عرف بيانات_الناتج: سند[مصفوفة[عائم]](الناتج.البيانات_ع32)؛

    طـرفية.اطبع("نتيجة ضرب المصفوفات (منقولة):\n[\n")؛

    // ملاحظة: بما أننا لا نملك وصولاً مباشراً لحقل ne في الموتر حالياً،
    // نعرف أن أبعاد الناتج هي صفوف_ب × صفوف_أ (منقولة)
    عرف صفوف_الناتج: صحيح = صفوف_ب؛
    عرف أعمدة_الناتج: صحيح = صفوف_أ؛

    عرف م: صحيح؛
    عرف ن: صحيح؛
    لكل ن = 0، ن < صفوف_الناتج، ++ن {
        طـرفية.اطبع("  ")؛
        لكل م = 0، م < أعمدة_الناتج، ++م {
            طـرفية.اطبع("%.2f "، بيانات_الناتج(ن * أعمدة_الناتج + م)~مثل[عائم[64]])؛
        }
        طـرفية.اطبع("\n")؛
    }
    طـرفية.اطبع("]\n")؛

    // 6. تحرير الذاكرة والخروج
    حرر(السياق)؛
}

رئيسية()؛
