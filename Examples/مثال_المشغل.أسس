اشمل "مـتم/مـصفوفة"؛
اشمل "مـتم/ذاكـرة"؛
اشمل "مـتم/طـرفية"؛
اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/Ggml"، "جـجمل.أسس")؛
استخدم مـتم؛
استخدم جـجمل؛

دالة رئيسية() {
    // تهيئة بيانات المصفوفات لإجراء عملية ضرب المصفوفات
    عرف صفوف_أ: صحيح = 4؛
    عرف أعمدة_أ: صحيح = 2؛
    عرف المصفوفة_أ: مـصفوفة[عائم]({
        2.0، 8.0،
        5.0، 1.0،
        4.0، 2.0،
        8.0، 6.0
    })؛

    عرف صفوف_ب: صحيح = 3؛
    عرف أعمدة_ب: صحيح = 2؛
    عرف المصفوفة_ب: مـصفوفة[عائم]({
        10.0، 5.0،
        9.0، 9.0،
        5.0، 4.0
    })؛

    // 1. تهيئة المشغل
    // حالياً نستخدم مشغل فلكان (يمكن إضافة دعم كودا لاحقاً)
    عرف المشغل: سند[مـشغل](مـشغل.هيئ_لفلكان(0))؛

    // حساب الحجم المطلوب للحجز
    عرف حجم_السياق: طـبيعي_متكيف = 0؛
    حجم_السياق += 2 * هات_تكلفة_الموتر()؛ // موتران
    // لا حاجة لحجز أي شيء آخر!

    // 2. حجز سياق جـجمل لتخزين البيانات الوصفية للموترات
    عرف السياق: سند[سـياق](هيئ(مـعطيات_تهيئة().{
        حجم_الذاكرة = حجم_السياق؛
        صوان_الذاكرة = 0؛
        بلا_حجز = 1؛ // سيتم حجز الموترات لاحقاً بواسطة احجز_موترات_مشغل()
    }))؛

    // 3. إنشاء البيانات الوصفية للموترات (أشكالها وأنواع بياناتها فقط)
    عرف الموتر_أ: سند[مـوتر](السياق.أنشئ_موترا(نـوع._ع32_، أعمدة_أ، صفوف_أ))؛
    عرف الموتر_ب: سند[مـوتر](السياق.أنشئ_موترا(نـوع._ع32_، أعمدة_ب، صفوف_ب))؛

    // 4. حجز صوان المشغل لتخزين جميع الموترات
    عرف الصوان: سند[صـوان_مشغل](السياق.احجز_موترات_مشغل(المشغل))؛

    // 5. نسخ بيانات الموترات من الذاكرة الرئيسية إلى صوان المشغل
    الموتر_أ.حدد_من_المشغل(المصفوفة_أ.صوان~مؤشر، 0، الموتر_أ.عدد_البايتات)؛
    الموتر_ب.حدد_من_المشغل(المصفوفة_ب.صوان~مؤشر، 0، الموتر_ب.عدد_البايتات)؛

    // 6. إنشاء بيان لعملية ضرب المصفوفات

    // إنشاء سياق مؤقت لبناء البيان
    // حجم البيان الافتراضي عادة 2048
    عرف حجم_البيان: طـبيعي_متكيف = هات_تكلفة_الموتر() * 2048 + هات_تكلفة_البيان()؛
    عرف سياق_البيان: سند[سـياق](هيئ(مـعطيات_تهيئة().{
        حجم_الذاكرة = حجم_البيان؛
        صوان_الذاكرة = 0؛
        بلا_حجز = 1؛ // سيتم حجز الموترات لاحقاً بواسطة حاجز البيان
    }))؛
    عرف البيان: سند[بـيان](سياق_البيان.أنشئ_بيانا())؛

    // الناتج = أ * ب^ت
    // ملاحظة: اضرب_مصفوفات(أ، ب) ==> سيتم قلب ب داخلياً
    // الناتج منقول
    عرف الناتج: سند[مـوتر](سياق_البيان.اضرب_مصفوفات(الموتر_أ، الموتر_ب))؛

    // إضافة موتر "الناتج" وجميع اعتمادياته إلى البيان
    ابن_توسيعا_أماميا(البيان، الناتج)؛

    // 7. إنشاء حاجز بيان لحساب البيان
    عرف الحاجز: سند[حـاجز_بيان](حـاجز_بيان.أنشئ(المشغل.هات_نوع_الصوان_المبدئي()))؛
    الحاجز.احجز_بيانا(البيان)؛

    // 8. تشغيل الحساب
    عرف عدد_المسالك: صحيح = 1؛ // عدد المسالك لعمليات التعدد المسلكي
    إذا المشغل.أللمعالج {
        المشغل.حدد_عدد_المسالك_للمعالج(عدد_المسالك)؛
    }
    المشغل.شغل_البيان(البيان)؛

    // 9. استرجاع النتائج (موترات الإخراج)
    // حجز ذاكرة لبيانات الناتج
    عرف بيانات_الناتج: مـصفوفة[عائم]؛
    بيانات_الناتج.احجز(صفوف_ب * صفوف_أ)؛
    // لأن بيانات الموتر مخزنة في صوان الجهاز، نحتاج لنسخها إلى الذاكرة الرئيسية
    الناتج.هات_من_المشغل(بيانات_الناتج.صوان~مؤشر، 0، الناتج.عدد_البايتات)؛

    // طباعة الناتج
    طـرفية.اطبع("نتيجة ضرب المصفوفات (منقولة):\n[\n")؛

    // ملاحظة: أبعاد الناتج هي صفوف_ب × صفوف_أ (منقولة)
    عرف صفوف_الناتج: صحيح = صفوف_ب؛
    عرف أعمدة_الناتج: صحيح = صفوف_أ؛

    عرف م: صحيح؛
    عرف ن: صحيح؛
    لكل ن = 0، ن < صفوف_الناتج، ++ن {
        طـرفية.اطبع("  ")؛
        لكل م = 0، م < أعمدة_الناتج، ++م {
            طـرفية.اطبع("%.2f "، بيانات_الناتج.صوان(ن * أعمدة_الناتج + م)~مثل[عائم[64]])؛
        }
        طـرفية.اطبع("\n")؛
    }
    طـرفية.اطبع("]\n")؛

    // 10. تحرير الذاكرة والخروج
    حرر(سياق_البيان)؛
    حـاجز_بيان.حرر(الحاجز)؛
    حرر(السياق)؛
    صـوان_مشغل.حرر(الصوان)؛
    مـشغل.حرر(المشغل)؛
}

رئيسية()؛
